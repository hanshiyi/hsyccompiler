#C0文法 编译器

###开发者：韩世依

###目标代码：mips汇编

###文法如下：

＜加法运算符＞ ::= +｜- <br>
＜乘法运算符＞  ::= *｜/<br>
＜关系运算符＞  ::=  <｜<=｜>｜>=｜!=｜==<br>
＜字母＞   ::= ＿｜a｜．．．｜z｜A｜．．．｜Z<br>
＜数字＞   ::= ０｜＜非零数字＞<br>
＜非零数字＞  ::= １｜．．．｜９<br>
＜字符＞    ::=  '＜加法运算符＞'｜'＜乘法运算符＞'｜'＜字母＞'｜'＜数字＞'<br>
＜字符串＞   ::=  "｛十进制编码为32,33,35-126的ASCII字符｝"                              <br>
＜程序＞    ::= ［＜常量说明＞］［＜变量说明＞］{＜有返回值函数定义＞|＜无返回值函数定义＞}＜主函数＞<br>
＜常量说明＞ ::=  const＜常量定义＞;{ const＜常量定义＞;}<br>
＜常量定义＞   ::=   int＜标识符＞＝＜整数＞{,＜标识符＞＝＜整数＞}
                            | char＜标识符＞＝＜字符＞{,＜标识符＞＝＜字符＞}
＜无符号整数＞  ::= ＜非零数字＞｛＜数字＞｝<br>
＜整数＞        ::= ［＋｜－］＜无符号整数＞｜０<br>
＜标识符＞    ::=  ＜字母＞｛＜字母＞｜＜数字＞｝<br>
＜声明头部＞   ::=  int＜标识符＞|char＜标识符＞<br>
＜变量说明＞  ::= ＜变量定义＞;{＜变量定义＞;}<br>
＜变量定义＞  ::= ＜类型标识符＞(＜标识符＞|＜标识符＞‘[’＜无符号整数＞‘]’){,(＜标识符＞|＜标识符＞‘[’＜无符号整数＞‘]’) }<br>
＜类型标识符＞      ::=  int | char<br>
＜有返回值函数定义＞  ::=  ＜声明头部＞‘(’＜参数＞‘)’ ‘{’＜复合语句＞‘}’<br>
＜无返回值函数定义＞  ::= void＜标识符＞‘(’＜参数＞‘)’‘{’＜复合语句＞‘}’<br>
＜复合语句＞   ::=  ［＜常量说明＞］［＜变量说明＞］＜语句列＞<br>
＜参数＞    ::= ＜参数表＞<br>
＜参数表＞    ::=  ＜类型标识符＞＜标识符＞{,＜类型标识符＞＜标识符＞}| ＜空＞<br>
＜主函数＞    ::= void main‘(’‘)’ ‘{’＜复合语句＞‘}’<br>
＜表达式＞    ::= ［＋｜－］＜项＞{＜加法运算符＞＜项＞}<br>
＜项＞     ::= ＜因子＞{＜乘法运算符＞＜因子＞}<br>
＜因子＞    ::= ＜标识符＞｜＜标识符＞‘[’＜表达式＞‘]’｜＜整数＞|＜字符＞｜＜有返回值函数调用语句＞|‘(’＜表达式＞‘)’<br>
＜语句＞    ::= ＜条件语句＞｜＜循环语句＞｜‘{’＜语句列＞‘}’｜＜有返回值函数调用语句＞; 
                      |＜无返回值函数调用语句＞;｜＜赋值语句＞;｜＜读语句＞;｜＜写语句＞;｜＜空＞;｜＜返回语句＞;<br>
＜赋值语句＞   ::=  ＜标识符＞＝＜表达式＞|＜标识符＞‘[’＜表达式＞‘]’=＜表达式＞<br>
＜条件语句＞  ::=  if ‘(’＜条件＞‘)’＜语句＞［else＜语句＞］<br>
＜条件＞    ::=  ＜表达式＞＜关系运算符＞＜表达式＞｜＜表达式＞ //表达式为0条件为假，否则为真<br>
＜循环语句＞   ::=  do＜语句＞while ‘(’＜条件＞‘)’ |for‘(’＜标识符＞＝＜表达式＞;＜条件＞;＜标识符＞＝＜标识符＞(+|-)＜步长＞‘)’＜语句＞<br>
＜步长＞::= ＜无符号整数＞  <br>
＜有返回值函数调用语句＞ ::= ＜标识符＞‘(’＜值参数表＞‘)’<br>
＜无返回值函数调用语句＞ ::= ＜标识符＞‘(’＜值参数表＞‘)’<br>
＜值参数表＞   ::= ＜表达式＞{,＜表达式＞}｜＜空＞<br>
＜语句列＞   ::=｛＜语句＞｝<br>
＜读语句＞    ::=  scanf ‘(’＜标识符＞{,＜标识符＞}‘)’<br>
＜写语句＞    ::=  printf‘(’＜字符串＞,＜表达式＞‘)’|printf ‘(’＜字符串＞‘)’|printf ‘(’＜表达式＞‘)’<br>
＜返回语句＞   ::=  return[‘(’＜表达式＞‘)’]<br>
